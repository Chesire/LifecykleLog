buildscript {
    ext.jacoco_version = '0.8.4'
    ext.kotlin_version = '1.3.50'
    repositories {
        google()
        jcenter()
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:3.5.0'
        classpath "org.jacoco:org.jacoco.core:$jacoco_version"
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
        classpath 'com.novoda:bintray-release:0.9.1'
    }
}

plugins {
    id "io.gitlab.arturbosch.detekt" version "1.0.1"
}

allprojects {
    repositories {
        google()
        jcenter()
    }
}

task clean(type: Delete) {
    delete rootProject.buildDir
}

detekt {
    toolVersion = "1.0.0-RC16"
    config = files("detekt.yml")
    input = files(
            "lifecyklelog/src/main/java",
            "lifecyklelog-sample/src/main/java"
    )
    filters = ".*/resources/.*,.*/build/.*"

    reports {
        xml {
            enabled = true
            destination = file("lifecyklelog/build/reports/detekt/detekt.xml")
        }
        html {
            enabled = true
            destination = file("lifecyklelog/build/reports/detekt/detekt.html")
        }
    }
}

ext.getVersionTag = {
        def stdout
        stdout = new ByteArrayOutputStream()
        exec {
            commandLine 'git', 'describe', '--tags'
            standardOutput = stdout
        }
        def longTag = stdout.toString().trim()

        return longTag.substring(longTag.indexOf('/') + 1, longTag.indexOf('-'))
    }
