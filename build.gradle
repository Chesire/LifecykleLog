buildscript {
    ext.jacoco_version = '0.8.5'
    ext.kotlin_version = '1.3.71'
    repositories {
        google()
        jcenter()
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:3.6.2'
        classpath "org.jacoco:org.jacoco.core:$jacoco_version"
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
        classpath 'com.novoda:bintray-release:0.9.2'
    }
}

plugins {
    id "io.gitlab.arturbosch.detekt" version "1.7.4"
    id "org.jlleitschuh.gradle.ktlint" version "9.2.1"
}

allprojects {
    repositories {
        google()
        jcenter()
    }
}

subprojects {
    apply plugin: "org.jlleitschuh.gradle.ktlint"
}

task clean(type: Delete) {
    delete rootProject.buildDir
}

detekt {
    toolVersion = "1.7.1"
    config = files("detekt.yml")
    input = files(
            "lifecyklelog/src/main/java",
            "lifecyklelog-sample/src/main/java"
    )
    reports {
        xml {
            enabled = true
            destination = file("lifecyklelog/build/reports/detekt/detekt.xml")
        }
        html {
            enabled = true
            destination = file("lifecyklelog/build/reports/detekt/detekt.html")
        }
    }
}

ext.getVersionTag = {
    def stdout = new ByteArrayOutputStream()
    exec {
        commandLine 'git', 'describe', '--tags', '--always'
        standardOutput = stdout
    }
    def longTag = stdout.toString().trim()

    if (longTag.contains('-')) {
        return longTag.substring(longTag.indexOf('/') + 1, longTag.indexOf('-'))
    } else {
        return longTag
    }
}

ext {
    lintrules_version = "1.1.3"
}
