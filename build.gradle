buildscript {
    ext.jacoco_version = '0.8.7'
    ext.kotlin_version = '1.5.30'
    repositories {
        google()
        jcenter()
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:4.2.2'
        classpath 'com.github.dcendents:android-maven-gradle-plugin:2.1'
        classpath "org.jacoco:org.jacoco.core:$jacoco_version"
        classpath 'com.jfrog.bintray.gradle:gradle-bintray-plugin:1.8.5'
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
    }
}

plugins {
    id "io.gitlab.arturbosch.detekt" version "1.18.1"
    id "org.jlleitschuh.gradle.ktlint" version "10.2.0"
}

allprojects {
    repositories {
        google()
        jcenter()
    }
}

subprojects {
    apply plugin: "org.jlleitschuh.gradle.ktlint"
}

task clean(type: Delete) {
    delete rootProject.buildDir
}

detekt {
    config = files("detekt.yml")
    input = files(
            "lifecyklelog/src/main/java",
            "lifecyklelog-sample/src/main/java"
    )
    reports {
        xml {
            enabled = true
            destination = file("build/reports/detekt/detekt.xml")
        }
        html {
            enabled = true
            destination = file("build/reports/detekt/detekt.html")
        }
    }
}

ext.getVersionTag = {
    def stdout = new ByteArrayOutputStream()
    exec {
        commandLine 'git', 'describe', '--tags', '--always'
        standardOutput = stdout
    }
    def longTag = stdout.toString().trim()

    if (longTag.contains('-')) {
        return longTag.substring(longTag.indexOf('/') + 1, longTag.indexOf('-'))
    } else {
        return longTag
    }
}

ext {
    lintrules_version = "1.2.6"
}
